// Hello World!
// 乗算でコード長を短くするバージョン

open import "std/std.bfml"

let gen_short_incr x n =
  let score (a, b) = a + Int:abs b + Int:abs (n - a * b) in
  let min_ab ab1 ab2 =
    if score ab1 < score ab2 then ab1 else ab2
  in
  let a, b =
    fix
      (fun loop prev a ->
        if Int:abs n <= a then prev
        else
          let curr = min_ab (a, n / a) (a, n / a + 1) in
          loop (min_ab prev curr) (a + 1)
      ) (1, n) 2
  in
  if Int:abs n <= score (a, b) + 10 then [ + x n ]
  else
    [ $alloc { y }
      + y a
      *gen_repeat y [ + x b ]
      + x (n - a * b)
    ]

let gen_puts2 s = [
  $alloc { c }
  *fix
    (fun loop prev l -> match l with
      | nil -> []
      | curr :: rest ->
          let out =
            if Int:abs (curr - prev) <= 3 + curr then [
              *gen_short_incr c (curr - prev)
            ] else [
              *gen_del c
              *gen_short_incr c curr
            ]
          in
          [ *out  . c  *loop curr rest ]
    ) 0 (String:to_list s)
]

let main = [ *gen_puts2 "Hello World!\n" ]